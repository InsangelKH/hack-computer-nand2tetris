class Keyboard {

    function void init() {
      return;
    }

    function char keyPressed() {
      return Memory.peek(24576);
    }

    function char readChar() {
      var char lastKey, currentKey;
      while (currentKey = 0) {
        let currentKey = Keyboard.keyPressed();
      }

      let lastKey = currentKey;
      while (lastKey = currentKey) {
        let currentKey = Keyboard.keyPressed();
      }

      if (lastKey < String.newLine()) {
        do Output.printChar(lastKey);
      }

      return lastKey;
    }

    function String readLine(String message) {
      var char c;
      var String line;

      let c = 0;
      let line = String.new(80);

      do Output.printString(message);

      while (true) {
        let c = Keyboard.readChar();
        if (c = String.newLine()) {
          do Output.println();
          return line;
        } else {
          if (c = String.backSpace()) {
            if (line.length() > 0) {
              do Output.backSpace();
              do line.eraseLastChar();
            }
          } else {
            do line.appendChar(c);
          }
        }
      }

      return line;
    }

    function int readInt(String message) {
      var String line;
      let line = Keyboard.readLine(message);
      return line.intValue();
    }
}